extends Sprite2D

@export var on_screen_offset: Vector2 = Vector2(0.5, -5.0)
@export var screen_margin: float = 4.0
@export var smoothing_speed: float = 8.0

var camera_node: Camera2D

func _ready():
	# Find the currently active camera in the scene at startup.
	camera_node = get_viewport().get_camera_2d()

func _process(delta: float) -> void:
	# prevent error when the camera_node doesn't get assigned on start
	if not camera_node:
		camera_node = get_viewport().get_camera_2d()
		return
	
	# get the position of the checkpoint
	var target_global_position: Vector2 = get_parent().global_position
	
	# get the size of the screen
	var viewport_dimensions: Vector2 = get_viewport().get_visible_rect().size	
	
	# calculating screen coordinates (converts world space into screen space)
	var screen_coordinates: Vector2 = (target_global_position - camera_node.global_position) * camera_node.zoom + viewport_dimensions * 0.5
	
	# define a rectangle representing the screen minus the margin.
	var screen_inset_rectangle: Rect2 = Rect2(Vector2.ZERO, viewport_dimensions).grow(-screen_margin)
	
	var target_display_position: Vector2
	var target_display_rotation: float

	# if the checkpoint is inside the screen
	if screen_inset_rectangle.has_point(screen_coordinates):
		target_display_position = target_global_position + on_screen_offset
		target_display_rotation = PI # Reset rotation (point up/default).
	else: # if the target is outside the screen
		# clamp the X and Y to ensure they stay within the screen margins.
		var clamped_x = clamp(screen_coordinates.x, screen_margin, viewport_dimensions.x - screen_margin)
		var clamped_y = clamp(screen_coordinates.y, screen_margin, viewport_dimensions.y - screen_margin)
		var clamped_screen_coords: Vector2 = Vector2(clamped_x, clamped_y)
		
		target_display_position = camera_node.global_position + (clamped_screen_coords - viewport_dimensions * 0.5) / camera_node.zoom
		
		# calculate rotation
		var vector_to_target: Vector2 = target_global_position - target_display_position
		target_display_rotation = vector_to_target.angle() + PI * 0.5
	
	# move and rotate the sprite gradually towards the new calculation.
	global_position = lerp(global_position, target_display_position, delta * smoothing_speed)
	rotation = lerp_angle(rotation, target_display_rotation, delta * smoothing_speed)
